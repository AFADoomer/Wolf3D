//Wolf3D Projectiles and Puffs

//Wolf3D Rocket
ACTOR WolfRocketBase
{
	PROJECTILE
	Radius 4
	Speed 14
	SeeSound "missile/fire"
	DeathSound "missile/hit"
	States
	{
		Spawn:
			MISL A 1 BRIGHT
			MISL A 1 BRIGHT A_SpawnItemEx("WolfRocketSmoke", 0, 0, 0)
			Loop
		Death:
			BAL3 CDE 4 BRIGHT A_Explode(16, 32, 1)
			Stop
	}
}

ACTOR WolfRocket : WolfRocketBase
{
	DamageType "WolfNazi"
}

ACTOR WolfRocketPlayer : WolfRocketBase
{
	Speed 20
	Damage 20
	+RANDOMIZE
	States
	{
		Death:
			BAL3 CDE 4 BRIGHT A_Explode
			Stop
	}
}

ACTOR WolfRocketSoD : WolfRocket
{
	Speed 10
	SeeSound "missile/sodfire"
}

ACTOR WolfRocketLost : WolfRocket
{
	SeeSound "missile/sodfire"
	DeathSound "missile/hit"
	States
	{
		Spawn:
			MISL B 1 BRIGHT
			MISL B 1 BRIGHT A_SpawnItemEx("WolfRocketSmokeLost", 0, 0, 0)
			Loop
		Death:
			BAL4 ABC 4 BRIGHT A_Explode(16, 32, 1)
			Stop
	}
}

ACTOR WolfRocketSmoke
{
	+NOBLOCKMAP
	+DROPOFF
	+NOGRAVITY
	- SOLID
	Height 5
	Radius 5
	Speed 0
	States
	{
		Spawn:
			TNT1 A 3
			RTRL ABC 2
		Death:
			RTRL D 2
			Stop
	}
}

ACTOR WolfRocketSmokeLost : WolfRocketSmoke
{
	States
	{
		Spawn:
			TNT1 A 3
			RTRL EFG 2
		Death:
			RTRL H 2
			Stop
	}
}

//Schabbs Syringe
ACTOR Syringe
{
	Projectile
	Speed 14
	Damage 6
	ExplosionDamage 6
	ExplosionRadius 10
	SeeSound "syringe/throw"
	DamageType "WolfNazi"
	DamageType "WolfNaziSyringe"
	States
	{
		Spawn:
			WB3P ABCD 3
			Loop
		Death:
			TNT1 AAA 4 A_Explode
			Stop
	}
}

//Hitler Ghost Fireballs
ACTOR GhostFireBallBase
{
	PROJECTILE
	+MTHRUSPECIES
	Radius 4
	Speed 2
	DamageType "Fire"
	SeeSound "flame/fire"
	States
	{
		Spawn:
			BAL3 AB 4 BRIGHT
			Loop
		Death:
			BAL3 A 0 BRIGHT A_Explode (16, 16, 1)
			Stop
	}
}

ACTOR GhostFireball : GhostFireballBase
{
	DamageType "WolfNazi"
}

//Used instead of GhostFireball when 'g_fastfireballs' cvar is true
// The original fireball speed in Wolf3D was dependant on processor speed
// instead of gametics...  So fireballs were faster on slower computers -
// the GhostFireBall actor above approximates the speed on a faster 
// machine; the one here is closer to the id-intended speed.
ACTOR FastGhostFireBall : GhostFireball
{
	Speed 8
}

//Flamethrower shot for player weapon
ACTOR WolfFlame : GhostFireballBase
{
	Speed 25
	Damage 5
	-MTHRUSPECIES
	States
	{
		Death:
			BAL3 CDE 4 BRIGHT
			Stop
	}
}

ACTOR SoDFireballBase
{
	PROJECTILE
	+MTHRUSPECIES
	Radius 4
	Speed 14
	DamageType "WolfNazi"
	States
	{
		Spawn:
			TNT1 A 0
		Fly:
			"####" ABCD 3 BRIGHT
			Loop
		Death:
			"####" ABCD 1 BRIGHT A_Explode(20, 16, 1)
			Stop
	}
}

//AoD Fireball
ACTOR GreenBall : SoDFireballBase
{
	SeeSound "aod/fire"
	States
	{
		Spawn:
			ADBL A 0 A_Jump(256, "Fly")
	}
}

//DI Fireball
ACTOR DIBall : SoDFireBallBase
{
	SeeSound "aod/fire"
	States
	{
		Spawn:
			DIBL A 0 A_Jump(256, "Fly")
	}
}

//Default Enemy BulletPuff (formerly used to make enemies immune to other enemies' shots)
ACTOR WolfPuff : BulletPuff
{
	Damagetype "WolfNazi"
}
